10 GOTO50
20 P=P+1:IFPEEK(P)THEN20ELSERETURN
30 IFINKEY$=""THEN30ELSERETURN
40 C$=INKEY$:IFC$=""THEN40ELSERETURN
50 PCLEAR 1:CLEAR 512,&H5FFF:FILES 0
60 GOSUB 10000:P=0:C$=""
70 REM TIME-CRITICAL CODE
80 REM ABOVE
90 REM
100 REM        TEST.BAS
110 REM
120 REM    A PROGRAM TO TEST
130 REM       6809-STRLEN
140 REM
200 REM     BY RICHARD JOHN
210 REM      FOSTER CAVELL
220 REM  (C) 2017, 2018, 2019,
230 REM            2025
240 REM
300 REM   HTTPS://GITHUB.COM/
310 REM     RICHARDCAVELL/
320 REM       6809-STRLEN
330 REM
400 REM   THIS PROGRAM IS FOR
410 REM   AN EMULATED TRS-80
420 REM    COLOR COMPUTER 2
430 REM   WITH DISK EXTENDED
440 REM       COLOR BASIC
450 REM    RUNNING IN XROAR
460 REM         OR MAME
470 REM
500 REM        IT NEEDS
510 REM    AT LEAST 32K AND
520 REM       AN EMULATED
530 REM       DISK DRIVE
540 REM
600 REM LANGUAGE SUBSCRIPTS
610 REM FOR THE SS$ ARRAY
620 E=0
630 D=1
640 F=2
650 S=3
800 REM CHANGE LN$ TO SET
810 REM THE DEFAULT LANGUAGE
820 REM E IS ENGLISH
830 REM D IS GERMAN
840 REM F IS FRENCH
850 REM S IS SPANISH
1000 LN$="E"
1050 REM CHANGE EA OR EB
1060 REM TO SIMULATE ERRORS
1070 REM 0 IS NORMAL
1080 REM 1 RETURNS OFF BY 1
1090 REM 2 RETURNS NO-END
1100 EA=0:EB=0
1200 REM OUR MACHINE LANGUAGE
1210 REM WORKING SPACE
1220 US=&H6000
1230 DEFUSR1=US+&H100
1240 DEFUSR2=US+&H180
1250 PS=US+&H200
1300 REM THE FILENAMES OF OUR
1310 REM MACHINE LANGUAGE
1320 REM PROGRAMS
1330 FL=4:DIM FI$(FL-1)
1340 FI$(0)="STRLEN"
1350 FI$(1)="NESTRLEN"
1360 FI$(2)="HELPER1"
1370 FI$(3)="HELPER2"
1400 REM THE SHORT STRINGS
1410 REM TO BE TESTED
1420 SL=6:DIM ST$(SL-1)
1430 ST$(0)="HELLO, WORLD!"
1440 ST$(1)="1234567890"
1450 ST$(2)="<>!@$%"
1460 ST$(3)="":FOR I=193 TO 220:ST$(3)=ST$(3)+CHR$(I):NEXT I
1470 ST$(4)=""
1480 ST$(5)="THIS IS A LONG STRING.  IT IS TOO LONG TO FIT ON ONE LINE."
1500 REM THE LONG STRINGS
1510 REM TO BE TESTED
1520 LL=4:DIM LS(LL-1)
1530 LS(0)=200
1540 LS(1)=500
1550 LS(2)=1024
1560 LS(3)=3000
1600 NL=4:DIM SS$(NL-1)
1700 AR=0
1710 CT=0:CI=0:CS=0
1720 LA=0:L=0
1730 Y=0
2000 REM TITLE SCREEN
2100 SS$(E)="6809-STRLEN TEST SUITE"
2110 SS$(D)="6809-STRLEN TESTLAUF"
2120 SS$(F)="SUITE DE TEST 6809-STRLEN"
2130 SS$(S)="SECUENCIA DE PRUEBA 6809-STRLEN"
2150 GOSUB 11800
2200 SS$(E)="BY RICHARD CAVELL"
2210 SS$(D)="VON RICHARD CAVELL"
2220 SS$(F)="PAR RICHARD CAVELL"
2230 SS$(S)="POR RICHARD CAVELL"
2250 GOSUB 11800
2300 GOSUB 10200
2400 S$="HTTPS://GITHUB.COM/"
2450 GOSUB 11200
2500 S$="RICHARDCAVELL/6809-STRLEN"
2550 GOSUB 11200
2600 GOSUB 10200
2700 S$="E = ENGLISH"
2710 GOSUB 10400
2720 S$="D = DEUTSCH"
2730 GOSUB 10400
2740 S$="F = FRANCAIS"
2750 GOSUB 10400
2760 S$="S = ESPANOL"
2770 GOSUB 10400
2800 GOSUB 10200
3000 SS$(E)="PRESS SPACE TO START"
3010 SS$(D)="PRESSE LEERTASTE ZU STARTEN"
3020 SS$(F)="APPUYEZ SUR LA BARRE"
3030 SS$(S)="PRESIONE LA BARRA DE ESPACIO"
3040 GOSUB 11800
3100 SS$(E)=""
3110 SS$(D)=""
3120 SS$(F)="D'ESPACE POUR DEMARRER"
3130 SS$(S)="PARA EMPEZAR"
3140 GOSUB 11800
3200 GOSUB 10200
3300 SS$(E)="(A FOR AUTORUN)"
3310 SS$(D)="(A FUER AUTORUN)"
3320 SS$(F)="(A FOR AUTORUN)"
3330 SS$(S)="(A PARA AUTOCORRER)"
3350 GOSUB 11800
3400 GOSUB 40
3500 IF C$="A" THEN 3900
3510 IF C$=" " THEN 4000
3520 IF C$="E" THEN 3700
3530 IF C$="D" THEN 3700
3540 IF C$="F" THEN 3700
3550 IF C$="S" THEN 3700
3600 GOTO 3400
3700 IF C$=LN$ THEN 3400
3800 LN$=C$
3810 GOSUB 10000
3820 GOTO 2000
3900 AR=1:REM FALLTHROUGH
4000 REM SECOND PAGE
4010 GOSUB 10000
4020 GOSUB 10200
4100 SS$(E)="PLEASE ENSURE THAT THE TEST"
4110 SS$(D)="BITTE STELLEN SIE SICHER,"
4120 SS$(F)="VEUILLEZ VOUS ASSURER QUE LE"
4130 SS$(S)="POR FAVOR ASEGURESE DE QUE"
4150 GOSUB 11800
4200 SS$(E)="SUITE DISK IS IN THE DRIVE"
4210 SS$(D)="DASS DIE TESTFOLGEN-DISK"
4220 SS$(F)="DISQUE DE LA SUITE DE TEST SE"
4230 SS$(S)="EL DISCO DE LA SECUENCIA DE"
4250 GOSUB 11800
4300 SS$(E)=""
4310 SS$(D)="IM LAUFWERK IST"
4320 SS$(F)="TROUVE DANS LE LECTEUR"
4330 SS$(S)="PRUEBA ESTE EN EL DISPOSITIVO"
4350 GOSUB 11800
4370 GOSUB 10200
4400 REM LOAD MACHINE
4410 REM LANGUAGE ROUTINES
4420 REM NAMED IN FI$()
4500 FOR FX=0 TO FL-1
4510 SS$(E)="   LOADING "
4520 SS$(D)="   LADE "
4530 SS$(F)="CHARGEMENT DE "
4540 SS$(S)="CARGANDO "
4550 GOSUB 11600
4600 PRINT FI$(FX);
4650 FOR I=LEN(FI$(FX)) TO 10
4660 PRINT ".";
4670 NEXT I
4700 LOADM FI$(FX)
4720 PRINT " ";
4800 SS$(E)="DONE!"
4810 SS$(D)="FERTIG!"
4820 SS$(F)="FINIS"
4830 SS$(S)="TERMINADO!"
4840 GOSUB 11400
4900 NEXT FX
4950 GOSUB 12400
5000 REM TEST SHORT STRINGS
5010 VL=0
5020 FOR SI=0 TO SL-1
5030 X$=ST$(SI)
5040 GOSUB 6000
5050 NEXT SI
5100 REM TEST VERY LONG STRINGS
5110 VL=1
5120 FOR SI=0 TO LL-1
5130 X=LS(SI)
5140 GOSUB 6000
5150 NEXT SI
5300 REM PRINT RESULTS
5310 GOSUB 10000
5320 SS$(E)="6809-STRLEN TEST SUITE"
5330 SS$(D)="6809-STRLEN TESTLAUF"
5340 SS$(F)="SUITE DE TEST 6809-STRLEN"
5350 SS$(S)="SECUENCIA DE PRUEBA 6809-STRLEN"
5360 GOSUB 11800
5400 SS$(E)="TOTAL SCORE"
5410 SS$(D)="PUNKTE INSGESAMT"
5420 SS$(F)="SCORE TOTAL"
5430 SS$(S)="PUNTOS TOTALES"
5440 GOSUB 11800
5450 GOSUB 10200
5500 S$=STR$(CI)+" ("
5510 S$=S$+STR$(INT((CS/CI)*100+0.5))
5520 S$=S$+"% )"
5550 SS$(E)=STR$(CS)+" OUT OF"+S$
5560 SS$(D)=STR$(CS)+" VON"+S$
5570 SS$(F)=STR$(CS)+" OUT OF"+S$
5580 SS$(S)=STR$(CS)+" DE"+S$
5590 GOSUB 11800
5600 GOSUB 10200
5700 SS$(E)="FINISHED!"
5710 SS$(D)="FERTIGGESTELLT!"
5720 SS$(F)="FINIS!"
5730 SS$(S)="FINALIZADO!"
5740 GOSUB 11400
5750 END
5760 REM NOTE PROGRAM ENDS HERE
6000 REM TEST LENGTH OF STRING
6010 GOSUB 10000
6100 CT=CT+1
6110 S$=MID$(STR$(CT),2)
6120 SS$(E)="TEST #"+S$
6130 SS$(D)="TEST #"+S$
6140 SS$(F)="TEST #"+S$
6150 SS$(S)="PRUEBA #"+S$
6160 GOSUB 11800
6200 IF VL=1 THEN GOTO 640
6210 GOSUB 10200
6220 SS$(E)="THE STRING IS : "
6230 SS$(D)="DER STRING IST : "
6240 SS$(F)="LA CHAINE EST : "
6250 SS$(S)="LA CADENA DE CARACTERES ES :"
6260 GOSUB 11400
6300 PRINT " ";CHR$(34);X$;CHR$(34)
6310 GOSUB 10200
6320 GOTO 6700
6400 SS$(E)="VERY LONG STRING TEST"
6410 SS$(D)="SEHR LANGER STRING TEST"
6420 SS$(F)="TEST DE TRES LONGUE CHAINE"
6430 SS$(S)="PRUEBA DE CADENA BIEN LARGA"
6440 GOSUB 11800
6500 SS$(E)="BE PATIENT!"
6510 SS$(D)="HABEN SIE GEDULD!"
6520 SS$(F)="VEUILLEZ PATIENTER!"
6530 SS$(S)="SEA PACIENTE!"
6540 GOSUB 11800
6550 GOSUB 10200
6600 SS$(E)="INTENDED LENGTH : "+STR$(X)
6610 SS$(D)="VORGESEHENE LAENGE : "+STR$(X)
6620 SS$(F)="LONGUEUR PREVUE : "+STR$(X)
6630 SS$(S)="LENGITUD ENTENDIDA : "+STR$(X)
6640 GOSUB 11400
6650 GOSUB 10200
6700 SS$(E)="CONSTRUCTING STRING..."
6710 SS$(D)="KONSTRUIERE STRING..."
6720 SS$(F)="CONSTRUCTION DE CHAINE.."
6730 SS$(S)="CONSTRUYENDO CADENA..."
6740 GOSUB 11600
6800 IF VL=1 THEN 6950
6810 LA=LEN(X$)
6820 IF LA=0 THEN 6900
6830 FOR I=0 TO (LA-1)
6840 POKE (PS+I), ASC(MID$(X$,I+1,1))
6850 NEXT I
6900 POKE (PS+LA), 0
6910 GOTO 7000
6950 I=USR2(X)
6960 LA=X
7000 SS$(E)="DONE!"
7010 SS$(D)="FERTIG!"
7020 SS$(F)="TERMINE"
7030 SS$(S)="LISTO!"
7040 GOSUB 11400
7050 GOSUB 10200
7100 SS$(E)="THE MEASURED LENGTH IS : "
7110 SS$(D)="DIE GEMESSENE LAENGE IST : "
7120 SS$(F)="LA LONGUEUR MESUREE EST : "
7130 SS$(S)="LA LONGITUD MEDIDA ES : "
7140 GOSUB 11400
7200 IF VL=1 THEN 7500
7300 SS$(E)=" (BASIC AUTO)  "
7310 SS$(D)=" (BASIC AUTO)  "
7320 SS$(F)=" (BASIC AUTO)  "
7330 SS$(S)=" (AUTO BASIC)  "
7340 GOSUB 11600
7400 LA=LEN(X$)
7410 PRINT LA
7500 SS$(E)=" (BASIC MANUAL)"
7510 SS$(D)=" (BASIC MANUAL)"
7520 SS$(F)=" (BASIC MANUEL)"
7530 SS$(S)=" (MANUAL BASIC)"
7540 GOSUB 11600
7600 P=PS-1
7610 GOSUB 20
7620 L=P-PS
7630 PRINT L;
7640 GOSUB 8000
7700 PRINT " (STRLEN)      ";
7710 GOSUB 13000
7720 IF L=-1 THEN PRINT " NE "; ELSE PRINT L;
7730 GOSUB 850
7800 PRINT " (NESTRLEN)    ";
7810 GOSUB 13200
7820 IF L=-1 THEN PRINT " NE "; ELSE PRINT L;
7830 GOSUB 8500
7840 GOSUB 12400
7900 RETURN
7910 REM TEST STRING
7920 REM SUBROUTINE ENDS HERE
8000 REM EXAMINE RESULT
8100 IF L<>LA THEN 8300
8200 SS$(E)="(MATCH)"
8210 SS$(D)="(GLEICH)"
8220 SS$(F)="(MATCH)"
8230 SS$(S)="(IGUAL)"
8240 GOSUB 11400
8250 RETURN
8300 SS$(E)="(ERROR!)"
8310 SS$(D)="(FEHLER!)"
8320 SS$(F)="(ERREUR!)"
8330 SS$(S)="(ERROR!)"
8340 GOSUB 11400
8350 GOSUB 10200
8400 SS$(E)="THERE IS AN INTERNAL ERROR"
8410 SS$(D)="ES IST EIN INTERNER FEHLER"
8420 SS$(F)="IL Y UNE ERREUR INTERNE"
8430 SS$(S)="HA UM ERRO INTERNO"
8440 GOSUB 11400
8450 END
8500 IF L<>LA THEN 8800
8550 CS=CS+1
8600 SS$(E)="(PASS)"
8610 SS$(D)="(PASS)"
8620 SS$(F)="(PASS)"
8630 SS$(S)="(PASA)"
8640 GOSUB 11400
8700 RETURN
8800 SS$(E)="(FAIL!)"
8810 SS$(D)="(FEHL!)"
8820 SS$(F)="(FAIL!)"
8830 SS$(S)="(FALLA!)"
8840 GOSUB 11400
8850 RETURN
10000 REM CLEAR SCREEN
10010 CLS
10020 RETURN
10200 REM BLANK LINE
10210 PRINT
10220 RETURN
10400 REM PRINT S$, INDENTED
10410 PRINT "          ";S$
10420 RETURN
11000 REM CHOOSE S$ BASED
11010 REM ON LANGUAGE
11020 IF LN$="E" THEN S$=SS$(E)
11030 IF LN$="D" THEN S$=SS$(D)
11040 IF LN$="F" THEN S$=SS$(F)
11050 IF LN$="S" THEN S$=SS$(S)
11060 RETURN
11200 REM PRINT S$, CENTRE-JUSTIFIED
11210 GOSUB 12200
11220 PRINT S$
11230 RETURN
11400 REM PRINT SS$
11410 REM BASED ON LANGUAGE
11420 GOSUB 11000
11430 PRINT S$
11440 RETURN
11600 REM PRINT SS$
11610 REM BASED ON LANGUAGE
11620 REM WITH NO NEWLINE
11630 GOSUB 11000
11640 PRINT S$;
11650 RETURN
11800 REM PRINT S$
11810 REM CENTRE-JUSTIFIED
11820 REM IF NOT BLANK
11830 GOSUB 11000
11840 GOSUB 12200
11850 IF S$<>"" THEN PRINT S$
11860 RETURN
12000 REM PRINT SS$
12010 REM CENTRE-JUSTIFIED
12020 REM IF NOT BLANK
12030 REM NO NEWLINE
12040 GOSUB 11000
12050 GOSUB 12200
12060 IF S$<>"" THEN PRINT S$;
12070 RETURN
12200 I=(32-LEN(S$))/2:IF I>=1 THEN PRINT STRING$(INT(I),32);
12210 RETURN
12400 REM IF AUTORUN IS NOT
12410 REM ON, ASK FOR
12420 REM KEYPRESS, THEN
12430 REM WAIT FOR IT
12440 IF AR=1 THEN RETURN
12450 PRINT
12500 SS$(E)="PRESS ANY KEY TO CONTINUE"
12510 SS$(D)="DRUECKEN SIE EINE BELIEBIGE"
12520 SS$(F)="APPUYEZ SUR N'IMPORTE QUELLE"
12530 SS$(S)="PRESIONE CUALQUIER TECLA"
12540 GOSUB 11800
12600 SS$(E)=""
12610 SS$(D)="TASTE UM FORTZUFAHREN"
12620 SS$(F)="TOUCHE POUR CONTINUER"
12630 SS$(S)="PARA CONTINUAR"
12640 GOSUB 12000
12650 GOTO 30
13000 REM USE STRLEN
13010 Y=0+EA*256
13020 GOTO 13400
13200 REM USE NESTRLEN
13210 Y=1+EB*256
13400 L=USR1(Y)
13410 CI=CI+1
13420 IF L<-1 THEN L=L+32768
13430 REM -1 IS NO-END
13440 REM ANY OTHER NEGATIVE
13450 REM RESULT SHOULD BE
13460 REM CORRECTED
13470 RETURN
